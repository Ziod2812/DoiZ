import QtQuick
import QtQuick.Controls
import Quickshell
import "."

Item {
    id: page

    Live {
        id: live
    }

    Head {
        id: head

        width: parent.width
        title: "THEME"
        info: live.get("dark", "dark").toUpperCase()
    }

    Flickable {
        anchors.left: parent.left
        anchors.right: parent.right
        anchors.top: head.bottom
        anchors.topMargin: 6
        anchors.bottom: parent.bottom
        contentWidth: width
        contentHeight: col.height
        clip: true
        boundsBehavior: Flickable.StopAtBounds

        ScrollBar.vertical: ScrollBar {
            policy: ScrollBar.AsNeeded

            contentItem: Rectangle {
                implicitWidth: 3
                radius: 2
                visible: parent && parent.size < 1
                color: Theme.accentColor
                opacity: 0.55
            }
        }

        Column {
            id: col

            width: parent.width - 6
            spacing: 6

            Sw {
                width: parent.width
                icon: "󰓑"
                title: "Dark mode"
                sub: "GTK, Qt, Firefox, Chromium, Electron and Thunar"
                checked: live.get("dark", "dark") === "dark"

                onToggled: {
                    Quickshell.execDetached(["bash", Theme.configHome + "/hypr/scripts/dark-mode.sh", "toggle"])
                    live.act(["get"])
                }
            }

            Sw {
                width: parent.width
                icon: "󰍜"
                title: "Waybar"
                sub: "Show or hide the top bar"
                checked: live.on("waybar")

                onToggled: live.act(["waybar", live.on("waybar") ? "off" : "on"])
            }

            Sw {
                width: parent.width
                icon: "󰂵"
                title: "Glass bar"
                sub: "Translucent Waybar"
                checked: live.get("glass", "off") === "on"

                onToggled: {
                    Quickshell.execDetached(["python3", Theme.configHome + "/waybar/scripts/bar-glass.py", "toggle"])
                    live.act(["get"])
                }
            }

            Sw {
                width: parent.width
                icon: "󰍹"
                title: "Wallpaper colors"
                sub: "Waybar, Quickshell, btop, cava and kitty follow the wallpaper"
                checked: live.on("dynamic")

                onToggled: live.act(["dynamic", live.on("dynamic") ? "off" : "on"])
            }

            Sw {
                width: parent.width
                icon: "󰝚"
                title: "Media popup"
                sub: "Player card that drops down when you hover the music module"
                checked: live.on("media_popup")

                onToggled: live.act(["set", "media_popup", live.on("media_popup") ? "0" : "1"])
            }

            Grid {
                width: parent.width
                columns: 2
                spacing: 6

                Repeater {
                    model: [
                        { key: "left_clock", label: "CAVA · LEFT, BEFORE TIME" },
                        { key: "left_workspaces", label: "CAVA · LEFT, AFTER WORKSPACES" },
                        { key: "right_cpu", label: "CAVA · RIGHT, BEFORE CPU" },
                        { key: "right_end", label: "CAVA · FAR RIGHT" },
                        { key: "center", label: "CAVA · CENTER (DEFAULT)" }
                    ]

                    Btn {
                        required property var modelData

                        width: (parent.width - 6) / 2
                        label: modelData.label
                        filled: live.get("cava_pos", "center") === modelData.key
                        tone: Theme.stateColor

                        onClicked: live.act(["set", "cava_pos", modelData.key])
                    }
                }
            }

            Head {
                width: parent.width
                title: "WINDOWS"
            }

            Sld {
                width: parent.width
                title: "Corner radius"
                unit: " px"
                to: 24
                value: Number(live.get("rounding", 4))

                onCommitted: (v) => live.act(["set", "rounding", String(v)])
            }

            Sld {
                width: parent.width
                title: "Border size"
                unit: " px"
                to: 8
                value: Number(live.get("border_size", 3))

                onCommitted: (v) => live.act(["set", "border_size", String(v)])
            }

            Sld {
                width: parent.width
                title: "Inactive window opacity"
                unit: " %"
                from: 40
                to: 100
                value: Number(live.get("inactive_opacity", 100))

                onCommitted: (v) => live.act(["set", "inactive_opacity", String(v)])
            }

            Sw {
                width: parent.width
                icon: "󰅶"
                title: "Blur"
                sub: "Blur behind floating windows"
                checked: live.on("blur")

                onToggled: live.act(["set", "blur", live.on("blur") ? "0" : "1"])
            }

            Sw {
                width: parent.width
                icon: "󰂏"
                title: "Window shadow"
                checked: live.on("shadow")

                onToggled: live.act(["set", "shadow", live.on("shadow") ? "0" : "1"])
            }

            Btn {
                width: parent.width
                label: "RESET TO DEFAULTS"
                tone: Theme.badColor

                onClicked: live.act(["reset"])
            }
        }
    }
}
